#!/usr/bin/perl
use warnings;
use strict;
use Sound::Library;
use FindBin qw($RealBin);
use Sound::SoundPlayer;
  
########################################################################
#
# Copyright Simon Andrews 2016
#
# This file is part of RasbellyPi.
#
# RasbellyPi is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Foobar is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with RasbellyPi.  If not, see <http://www.gnu.org/licenses/>.
#
########################################################################


############ DEBUG ################
# Set this to 1 to enable debugging
#
my $debug = 1;
###################################


my $magic_number = read_magic_number();


my $library = new Sound::Library("$RealBin/Sounds");

{

  my $year = (localtime())[5]+1900;

  if ($year >=2016) {
    # They've probably set the clock
    (new Sound::SoundPlayer()) ->play_sound("$RealBin/Notifications/ready.mp3");
  }
  else {
    # There's no clock set
    (new Sound::SoundPlayer()) ->play_sound("$RealBin/Notifications/ready_no_clock.mp3");
  }
}

# Main event loop

while (1) {

  # This command will block until a button press is received
  my $number = `RFSniffer`;
  
  chomp $number;

  $debug and warn "Received a signal with magic number '$number'\n";
  
  unless ($number =~ /^\d+$/) {
    warn "Output of RFSniffer was not a number, it was '$number'";
    next;
  }

  
  if ($magic_number == 0 || $magic_number == $number) {
    # Play a sound
    $library -> play_sound();
    $debug and warn "Key was pressed\n";
  }
  else {
    $debug and warn "Got a signal from someone else's bell\n";
  }
    

}

sub read_magic_number {

  open (my $magic, "$RealBin/magic_number.txt") or do {
    warn "Couldn't read magic number file: $! - will respond to any bell - not just yours\n";
    return 0;
  };

  my $line = <$magic>;
  close $magic;

  unless ($line) {
    warn "First line of magic_number.txt was empty - will respond to any bell - not just yours\n";
    return 0;
  }
  
  if ($line =~ /^Magic number =\s*(\d+)/) {
    return $1;
  }
  else {
    warn "First line of magic_number.txt didn't have the expected format - will respond to any bell - not just yours\n";
    return 0;    
  }
  
}





